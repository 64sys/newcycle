' Gambas class file

'
' Nuevo ciclo
' Interfaz para programa de la terminal
'
' Copyright (C) Martín Belmonte.
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
'

Private intYear As Integer
Private intInit As Integer
Private intQty As Integer
Private strSeed As String
Private strPscr As String
Private strProd As String
Private pdfFile As New PdfDocument
Private intW As Integer
Private intH As Integer
Private bolLoaded As Boolean

Public Sub Form_Open()

  Dim int As Integer
  Dim strSize As String
  Dim strOrie As String
  Dim strLang As String
  Dim strDay As String

  hzsData.Layout = [1, 4, 1]

  intYear = CInt(Format(Now(), "yyyy"))

  For int = 0 To 10
    cmoYear.List.Add(CStr(intYear - 5 + int))
  Next

  cmoYear.Text = CStr(intYear)

  For int = 1 To 12
    cmoMonthInit.List.Add(CStr(int))
  Next

  cmoMonthInit.Text = "1"

  For int = 1 To 12
    cmoMonthQty.List.Add(CStr(int))
  Next

  cmoMonthQty.Text = "12"

  For int = 0 To GEFStarter.stxPaper.Max
    strSize = GEFStarter.stxPaper[int][1]
    cmoPaperSize.Add(strSize)
    Select CStr(int)
      Case Settings[GEFStarter.stxProgKey[0], ""]
        GEFStarter.stxProgVal[0] = CStr(int)
        cmoPaperSize.Text = strSize
    End Select
  Next

  For int = 0 To GEFStarter.stxOrie.Max
    strOrie = GEFStarter.stxOrie[int][1]
    cmoPaperOrientation.Add(strOrie)
  Next
  cmoPaperOrientation.Text = GEFStarter.stxOrie[1][1]
  GEFStarter.stxProgVal[1] = 0

  For int = 0 To GEFStarter.stxLang.Max
    strLang = GEFStarter.stxLang[int][1]
    cmoPaperTextLanguage.Add(strLang)
    Select String.LCase(GEFStarter.stxLang[int][0])
      Case "spanish"
        GEFStarter.stxProgVal[2] = CStr(int)
        cmoPaperTextLanguage.Text = strLang
    End Select
  Next

  For int = 0 To GEFStarter.stxDay.Max
    strDay = GEFStarter.stxDay[int][1]
    cmoFirstDay.Add(strDay)
    Select String.LCase(GEFStarter.stxDay[int][0])
      Case "monday"
        GEFStarter.stxProgVal[3] = CStr(int)
        cmoFirstDay.Text = strDay
    End Select
  Next

End

Public Sub tobAbout_Click()

  GEFAbout.ShowModal()

End

Public Sub mnuAbout_Click()

  GEFAbout.ShowModal()

End

Public Sub tobPrint_Click()

  If Exist(strProd) Then
    CalendarPrint(strProd)
  Endif

End

Public Sub CalendarPrint(strPath As String) ''Imprime el pdf que se le pasa como parametro

  Shell "lp -o media=Custom.211x297mm -o DefaultBRDuplex=DuplexNoTumble -o BRResolution=600dpi " & strPath

End

Public Sub mnuExit_Click()

  Me.Close

End

Public Sub Form_Close()

  If GEFStarter.Terminator() = 1 Then
    Me.Close
  Else
    Message.Error("No se han podido guardar los parámetros del programa")
  Endif

End

Public Sub mnuNew_Click()

End

'## Función principal que interactiua con pcal

Public Function CreateConf() ''Crea el archivo de configuracion que se utilizara para crear el calendario

  If cmoPaperSize.Index > -1 Then
    GEFStarter.stxProgVal[0] = cmoPaperSize.Index
  Endif

  If cmoPaperOrientation.Index > -1 Then
    GEFStarter.stxProgVal[1] = cmoPaperOrientation.Index
  Endif

  If cmoPaperTextLanguage.Index > -1 Then
    GEFStarter.stxProgVal[2] = cmoPaperTextLanguage.Index
  Endif

  If cmoFirstDay.Index > -1 Then
    GEFStarter.stxProgVal[3] = cmoFirstDay.Index
  Endif

  GEFStarter.stxProgVal[9] = cmoYear.Text
  GEFStarter.stxProgVal[10] = cmoMonthInit.Text
  GEFStarter.stxProgVal[11] = cmoMonthQty.Text

  strSeed = GEFStarter.stxProgVal[7]
  strPscr = GEFStarter.stxProgVal[8] &/ GEFStarter.stxProgVal[9] & ".ps"
  strProd = GEFStarter.stxProgVal[8] &/ GEFStarter.stxProgVal[9] & ".pdf"

  If Exist(strPscr) = True Then
    Kill strPscr
  Endif

  If Exist(strProd) = True Then
    Kill strProd
  Endif

  Print strSeed
  Print strProd

End

Public Function LoadCalendar()

  Dim strCmd As String
  Dim img As Image
  Dim pio As Picture
  Dim intNpage, intWidthPage, intHeightPage As Integer
  Dim strPage As String
  Dim strOrie As String
  Dim strLang As String
  Dim strDay As String

  strPage = GEFStarter.stxPaper[CInt(GEFStarter.stxProgVal[0])][0]
  strOrie = GEFStarter.stxOrie[CInt(GEFStarter.stxProgVal[1])][0]
  strLang = GEFStarter.stxLang[CInt(GEFStarter.stxProgVal[2])][2]
  strDay = GEFStarter.stxDay[CInt(GEFStarter.stxProgVal[3])][0]

  tobPrint.Enabled = False

  CreateConf()
  strCmd = "pcal"
  strCmd &= " -P " & strPage

  Select strOrie
    Case "landscape"
      strCmd &= " -l"
  End Select
  strCmd &= " -a " & strLang
  strCmd &= " -F " & strDay
  strCmd &= " -d Times-Roman/18"
  strCmd &= " -t Times-Roman/30"
  strCmd &= " -n Times-Roman/10"
  strCmd &= " -o " & strPscr
  strCmd &= " " & GEFStarter.stxProgVal[10]
  strCmd &= " " & GEFStarter.stxProgVal[9]
  strCmd &= " " & GEFStarter.stxProgVal[11]

  Print strCmd

  Shell strCmd Wait

  Shell "ps2pdf " & strPscr & " " & strProd Wait

  pdfFile.Open(strProd)

  If pdfFile.Ready = True Then

    intNpage = 1
    intWidthPage = pdfFile[intNpage].Width
    intHeightPage = pdfFile[intNpage].Height

    img = pdfFile[intNpage].Image
    img.Stretch(intW, intH)
    pio = img.Picture

    Wait 0.01
    pix.Picture = pio
    pix.W = pnlCalendar.W
    pix.H = pnlCalendar.W * 0.7071
    pix.Refresh
    tobPrint.Enabled = True
  Else
    Message.Error("Error al intentar cargar el pdf")
  Endif

End

Public Sub Parameters_Change()

  If bolLoaded = True Then
    CreateConf()
    LoadCalendar()
  Endif

End

Public Sub Form_Activate()

  intW = (Me.W / 6) * 4
  intH = intW * 0.7071

  'bolLoaded = True

  CreateConf()

  LoadCalendar()

End

Public Sub mnuReset_Click()

  Reset()

End

Public Sub tobReset_Click()

  Reset()

End

Public Sub Reset()

  If Message.Question(
      ("¿Desea restablecer los parámetros originales?") & gb.NewLine &
      ("Tenga en cuenta que si acepta los cambios que usted haya realizado en la configuracion se perderan") & ".", "Aceptar", "Cancelar") = 1 Then
    If Exist(GEFStarter.strAppPath) = True Then
      Shell "rm -r " & GEFStarter.strAppPath Wait
      Me.Close
      GEFStarter.Main()

    Endif

  Endif

End

Public Sub mnuSaveas_Click()

  Saveas(strProd)

End

Public Function Saveas(strFile As String)

  Dim strSave As String

  If Exist(strFile) = True Then
    If Exist(GEFStarter.stxProgVal[12]) = True Then
      Dialog.Path = GEFStarter.stxProgVal[12]
    Else
      Dialog.Path = User.Home &/ "."
    Endif
    Dialog.Title = ("Guardar como")
    Dialog.Filter = ["*.pdf", "Portable document format"]
    Dialog.SaveFile()
    strSave = Dialog.Path

    Select File.Ext(strSave)

      Case "pdf", "PDF"
        GEFStarter.stxProgVal[12] = File.Dir(strSave)

        Copy strFile To strSave

      Case Else
        Message.Info("Debe asignar una extensión de archivo adecuada")
    End Select
  Endif

End

Public Sub tobSaveAs_Click()

  Saveas(strProd)

End
